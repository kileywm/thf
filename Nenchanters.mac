#include Spell_routines.inc
#include actions.inc

Sub Main
  |----Detect Items------------------------------------------------------
  #include clickies.inc

  |----Detect Group Leaders------------------------------------------------------
  #include groups.inc

  /declare Command string outer
  /declare Action string outer Nothing
  /declare ChangeAction bool outer FALSE
  /declare NewTarget bool outer TRUE
  /declare RaidTarg int outer
  /declare NewRaidTarg int outer
  /declare RaidTargType string outer
  /declare HealTarg int outer
  /declare Arg1 string outer
  /declare Arg2 string outer
  /declare Arg3 string outer
  /declare Arg4 string outer

  /declare ExpCheck timer outer ${Math.Calc[${Math.Rand[100]}+200+${Me.Class.ID}]}
  /declare j int outer 0
  /if (!${Defined[Listening]}) /declare Listening bool outer TRUE
  /declare MaintainIllusion bool outer FALSE
  /declare ClassType string outer
  /declare ChangeMelody bool outer TRUE
  /declare Selos bool outer TRUE
  /declare DiscActivate timer outer
  /declare IntroAttack bool outer TRUE
  /declare TryAttempts int outer 0
  /declare AA1Timer timer outer 0
  /declare LastZone int outer ${Zone.ID}
  /declare ZoneChange bool outer FALSE

  /declare PetStatus int outer 0
  /declare PetBaneWeap timer outer 0
  /declare AutoPet bool outer TRUE
  /declare AutoPosition bool outer TRUE
  /declare AutoDisc bool outer FALSE

  /declare DebuffTimer timer outer 0
  /declare Dot1Timer timer outer 0
  /declare Dot2Timer timer outer 0
  /declare Dot3Timer timer outer 0
  /declare Dot4Timer timer outer 0
  /declare Dot5Timer timer outer 0
  /declare Dot6Timer timer outer 0
  /declare PurgeCrapBuffsTimer timer outer 0

  /declare TankCheck timer outer 0

  /echo [${Time.Time24}] Macro active...
  /call DetectGems
  /call CheckGems
  /call DetectSpells
  /call Settings
  /if (${Melee.Enable}) /melee off

  :Loop
  /if (${MacroQuest.GameState.Equal[INGAME]}) {
    /doevents
    /if (${Command.Length}>0) {
      /if (${Listening}) {
        /varset Action ${Command}
        /varset ChangeAction TRUE
      }
      /varset Command
    }
    /if (${NewRaidTarg}>0) {
      /if (${Listening}) {
        /varset RaidTarg ${NewRaidTarg}
      }
      /varset NewRaidTarg 0
    }

    /if ((${Zone.ID} && ${Zone.ID}!=${LastZone}) || ${ZoneChange}) {
      /varset ZoneChange TRUE
      /call ZoneChange
    }

    /if (${Action.NotEqual[Nothing]}) {
      /if (${ChangeAction}) /varset ChangeAction FALSE
      /if (${Action.NotEqual[Idle]} && ${MacroQuest.GameState.Equal[INGAME]}) /call ${Action}
      /if (!${ChangeAction}) {
        /if (${Cursor.ID} && ${Action.Equal[Combat]}) /autoinv
        /doevents
        /if (!${ChangeAction} && ${MacroQuest.GameState.Equal[INGAME]}) /call CheckGems
        /doevents
        /if (!${ChangeAction} && ${MacroQuest.GameState.Equal[INGAME]}) /call CheckLevels
        /doevents
        /if (!${ChangeAction} && ${MacroQuest.GameState.Equal[INGAME]}) /call CheckStatus
        /doevents
        /if (!${ChangeAction} && ${MacroQuest.GameState.Equal[INGAME]}) /call CheckBuffs
        /doevents
        /if (!${ChangeAction} && ${MacroQuest.GameState.Equal[INGAME]}) /call Pet
      }
    }
  }
  /goto :Loop

Sub CheckLevels
  /if (${ExpCheck}==0) /call BalExp

|--Survival---------------------------------------------------------------------
  /if (${Me.Pet.ID} && ${Action.Equal[Combat]}) {
    /if (${PetStatus}==0 && ${Me.Pet.PctHPs}<50) {
      /pet hold
      /varset PetStatus 1
    } else /if (${PetStatus}==1 && ${Me.Pet.PctHPs}>50) {
      /squelch /tar ID ${RaidTarg}
      /delay 3
      /if (${AutoPet}) /pet attack
      /varset PetStatus 0
    }
  }

  /if (${Action.Equal[Combat]} && ${Me.PctHPs}<50 && ${Defined[NadoxGloves]} && ${NadoxGlovesTimer}==0 && ${Target.Distance}<${Math.Calc[${NadoxGlovesRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Casting ${NadoxGloves}
    /call cast ${NadoxGloves}
    /if (${Macro.Return.Equal[CAST_SUCCESS]}) {
      /varset NadoxGlovesTimer 4800
    } else {
      /varset NadoxGlovesTimer 600
    }
    /return
  }

|--Mana Clickies----------------------------------------------------------------

  /if (${Defined[ManaBracer]} && ${ManaBracerTimer}==0 && ${Action.NotEqual[Killnear]}) {
    /echo [${Time.Time24}] Clicking ${ManaBracer}
    /call cast ${ManaBracer} item
    /varset ManaBracerTimer 96
  }
  /if (${Defined[AvilaineStaff]} && ${AvilaineStaffTimer}==0 && ${Action.NotEqual[Killnear]}) {
    /echo [${Time.Time24}] Clicking ${AvilaineStaff}
    /call cast ${AvilaineStaff} item
    /varset AvilaineStaffTimer 300
  }
  /if (${Defined[ZenurixStone]} && ${Me.PctMana}<40 && ${FindItem[${ZenurixStone}].Timer}==0) {
    /echo [${Time.Time24}] Clicking ${ZenurixStone}
    /call cast ${ZenurixStone} item
  }
  /if (${Defined[ArbiterBelt]} && ${Me.PctMana}<40 && ${ArbiterBeltTimer}==0) {
    /echo [${Time.Time24}] Clicking ${ArbiterBelt}
    /call cast ${ArbiterBelt} item
    /varset ArbiterBeltTimer 600
  }
  /if (${Defined[ManaTears]} && ${Me.PctMana}<40 && ${ManaTearsTimer}==0) {
    /echo [${Time.Time24}] Clicking ${ManaTears}
    /call cast ${ManaTears} item
    /varset ManaTearsTimer 4200
  }
  /if (${Defined[T8Chest]} && ${Me.PctMana}<40 && ${FindItem[${T8Chest}].Timer}==0 && ${Target.PctHPs}<95 && ${Target.Distance}<${Math.Calc[${T8ChestRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Clicking ${T8Chest}
    /call cast ${T8Chest} item
  }
/return

Sub CheckGems
  /if ((${Gem1} && !${Me.Gem[1].ID}) || (${Gem2} && !${Me.Gem[2].ID}) || (${Gem3} && !${Me.Gem[3].ID}) || (${Gem4} && !${Me.Gem[4].ID}) || (${Gem5} && !${Me.Gem[5].ID}) || (${Gem6} && !${Me.Gem[6].ID}) || (${Gem7} && !${Me.Gem[7].ID}) || (${Gem8} && !${Me.Gem[8].ID}) || (${Gem9} && !${Me.Gem[9].ID})) {
    /echo [${Time.Time24}] Memming spells
    /memspellset ${SpellSet}
    /delay 5
    :WaitForMem
    /if (${Window[SpellBookWnd].Open}) {
      /delay 1
      /goto :WaitForMem
    }
    /call DetectSpells
  }
/return

Sub CheckBuffs
  /if (!${Defined[Draekon]}) {
    /if (${Defined[CritShoulder]} && !${Me.Buff[${CritShoulderBuff}].ID}) {
      /echo [${Time.Time24}] Rebuffing ${CritShoulderBuff}
      /call cast ${CritShoulder} item
      /return
    }
    /if (${Defined[CritMask]} && !${Me.Buff[${CritMask}].ID}) {
      /echo [${Time.Time24}] Rebuffing ${CritMaskBuff}
      /call cast ${CritMask} item
      /return
    }
    /if (${Defined[LichClicky]} && !${Me.Buff[${LichBuff}].ID}) {
      /echo [${Time.Time24}] Buffing ${LichBuff}
      /call cast ${LichClicky} item
      /return
    }
    /if (${Defined[BPIllusion]} && ${MaintainIllusion} && !${Me.Buff[${BPIllusionBuff}].ID} && !${Me.Song[Form of the Moon Cursed].ID} ) {
      /echo [${Time.Time24}] Buffing ${BPIllusion}
      /call cast ${BPIllusion} item
      /return
    }
    /if (${Defined[SpellHateNeck]} && !${Me.Buff[${SpellHateNeckBuff}].ID}  && ${SpellHateNeckTimer}==0) {
      /echo [${Time.Time24}] Buffing ${SpellHateNeckBuff}
      /call cast ${SpellHateNeck} item
      /varset SpellHateNeckTimer 6000
      /return
    }
    /if (!${Me.Buff[Mana Inferno Rk. III].ID} && !${Me.Buff[Mana Inferno Rk. II].ID} && !${Me.Buff[Mana Inferno Rk. I].ID} && ${Me.PctMana}>30) {
      /if (${Group} || ${Raid.Members}) {
        /for j 1 to ${GroupLead.Size}
          /if (${Spawn[pc ${GroupLead[${j}]}].ID}) {
            /call ShortBuff
          } else {
            /echo [${Time.Time24}] ${GroupLead[${j}]} not in zone.
            /next j
          }
        /return
      } else {
        /echo Buffing self with ${InfernoBuff}
        /call WaitForSpellReady ${InfernoBuff}
        /call cast ${InfernoBuff}
      }
    }

    /if (${Defined[DestChaosMask]} && !${Me.Buff[${DestChaosMaskBuff}].ID}) {
      /echo [${Time.Time24}] Clicking ${DestChaosMask}
      /call cast ${DestChaosMask} item
      /return
    }
    /if (${Defined[WoSPet]} && !${Me.Buff[${WoSPetBuff}].ID}  && ${WoSPetTimer}==0) {
      /declare HasPet bool local FALSE
      /if (${Me.Pet.ID}) /varset HasPet TRUE
      /echo [${Time.Time24}] Buffing ${WoSPetBuff}
      /call cast ${WoSPet} item
      /if (!${Macro.Return.Equal[CAST_INTERRUPTED]}) {
        /varset WoSPetTimer 6000
        /delay 5
        /if (!${HasPet}) /pet get lost
      } else {
        /varset WoSPetTimer 300
      }
      /return
    }
  }

  /if (!${Me.Buff[Visions of Power Rk. III].ID} && !${Me.Buff[Visions of Power Rk. II].ID} && !${Me.Buff[Visions of Power Rk. I].ID} && ${Me.SpellReady[${TwincastBuff}]} && ${Me.PctMana}>30) {
    /echo [${Time.Time24}] Buffing ${TwincastBuff}
    /call cast ${TwincastBuff}
    /return
  }

/return

Sub Combat
  /if (!${Spawn[id ${RaidTarg}].ID} || ${Spawn[id ${RaidTarg}].Type.Equal[Corpse]}) {
    /if (${Action.Equal[Combat]}) {
      /varset Action Hold
    }
    /varset ChangeAction TRUE
    /call CheckBoss
    /return
  } else /if (${Spawn[id ${RaidTarg}].ID} &&  (!${Target.ID} || ${Target.ID}!=${RaidTarg})) {
    /squelch /tar ID ${RaidTarg}
    /delay 3
  }

  /if (${NewTarget}) {
    /echo [${Time.Time24}] Attacking...
    /call CheckBoss
    /varset IntroAttack TRUE
    /varset NewTarget FALSE
    /varset DebuffTimer 0
    /if (${AutoPet}) /pet attack

    /if (!${Defined[MeleeDebuffed]}) /declare MeleeDebuffed bool local FALSE
    /if (!${Defined[SpellDebuffed]}) /declare SpellDebuffed bool local FALSE
    /if (!${Defined[ResistDebuffed]}) /declare ResistDebuffed bool local FALSE
    /if (!${Defined[EpicDebuffed]}) /declare EpicDebuffed bool local FALSE

    /varset MeleeDebuffed bool outer FALSE
    /varset SpellDebuffed bool outer FALSE
    /varset ResistDebuffed bool outer FALSE
    /varset EpicDebuffed bool outer FALSE

    /if (!${Defined[Epic20]} && !${Defined[Epic15]}) /varset EpicDebuffed TRUE
  }

  /if (${Action.Equal[Combat]} && (${Target.Type.NotEqual[NPC]} || !${Target.ID})) {
    /varset ChangeAction TRUE
    /varset Action Hold
    /call CheckBoss
    /return
  }

  /if (${IntroAttack}) {
    /if (${Spawn[id ${RaidTarg}].ID} && ${Spawn[id ${RaidTarg}].Type.NotEqual[Corpse]}) {
      /call CheckStatus
      /call WaitForSpellReady ${MeleeVuln}
      /call cast ${MeleeVuln}
      /if (${Macro.Return.Equal[CAST_SUCCESS]}) {
        /varset MeleeDebuffed TRUE
      }
    }
    /varset IntroAttack FALSE
    /varset TryAttempts 0
  }

  /if (${DebuffTimer}==0 && ${Action.Equal[Combat]}) {

    /echo [${Time.Time24}] Debuffing ${Target.CleanName}
    :TryDebuff
    /if (${Action.NotEqual[Combat]} || ${ChangeAction}) /return
    /if (${TryAttempts}>5 || (!${Spawn[id ${RaidTarg}].ID} || ${Spawn[id ${RaidTarg}].Type.Equal[Corpse]})) /return
    /if (!${ResistDebuffed} && ${Target.Distance}<${Math.Calc[${Spell[${ResistDebuff}].Range}*${RangeMod}]}) {
      /varset TryAttempts 0
      /call WaitForSpellReady ${ResistDebuff}
      /call cast ${ResistDebuff}
      /if (!${Macro.Return.Equal[CAST_INTERRUPTED]} && !${Macro.Return.Equal[CAST_FIZZLE]}) {
        /varset ResistDebuffed TRUE
      } else {
        /echo [${Time.Time24}] Trying ${ResistDebuff} again
        /varcalc TryAttempts ${TryAttempts}+1
        /goto :TryDebuff
      }
    }
    /if (!${EpicDebuffed} && ${Target.Distance}<${Math.Calc[${Epic20Range}*${RangeMod}]} && ${Action.Equal[Combat]}) {
      /if (${Defined[Epic20]} && ${Epic20Timer}==0) {
        /call cast ${Epic20} item
        /varset Epic20Timer 120
      } else /if (${Defined[Epic15]} && ${Epic15Timer}==0) {
        /call cast ${Epic15} item
        /varset Epic15Timer 120
      }
      /if (${Macro.Return.Equal[CAST_SUCCESS]}) {
        /varset EpicDebuffed TRUE
      } else {
        /echo [${Time.Time24}] Trying Epic Debuff Again
        /varcalc TryAttempts ${TryAttempts}+1
        /goto :TryDebuff
      }
    }
    /if (!${MeleeDebuffed} && ${Target.Distance}<${Math.Calc[${Spell[${MeleeVuln}].Range}*${RangeMod}]} && ${Action.Equal[Combat]}) {
      /call WaitForSpellReady ${MeleeVuln}
      /call cast ${MeleeVuln}
      /if (${Macro.Return.Equal[CAST_SUCCESS]}) {
        /varset MeleeDebuffed TRUE
      } else {
        /echo [${Time.Time24}] Trying ${MeleeVuln} again
        /varcalc TryAttempts ${TryAttempts}+1
        /goto :TryDebuff
      }
    }
    /if (!${SpellDebuffed} && ${Target.Distance}<${Math.Calc[${Spell[${SpellVuln}].Range}*${RangeMod}]} && ${Action.Equal[Combat]}) {
      /call WaitForSpellReady ${SpellVuln}
      /call cast ${SpellVuln}
      /if (${Macro.Return.Equal[CAST_SUCCESS]}) {
        /varset SpellDebuffed TRUE
      } else {
        /echo [${Time.Time24}] Trying ${SpellVuln} again
        /varcalc TryAttempts ${TryAttempts}+1
        /goto :TryDebuff
      }
    }
    /varset DebuffTimer 6000
    /return
  }

  /if (${IntroAttack}) {
    /varset IntroAttack FALSE
  }

  /if (${Defined[RallosZek]} && ${Defined[ZekBane]} && ${ZekTimer}==0 && ${Target.Distance}<${Math.Calc[${ZekBaneRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Using ${ZekBane} on Rallos Zek!
    /call cast ${ZekBane} item
    /varset ZekTimer 41
    /return
  }

  /if (${Defined[VulthrixWhip]} && ${FindItem[${VulthrixWhip}].Timer}==0 && ${Target.Distance}<${Math.Calc[${VulthrixWhipRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Clicking ${VulthrixWhip}
    /call cast ${VulthrixWhip} item
    /return
  }

  /if (${Defined[NadoxShield]} && ${FindItem[${NadoxShield}].Timer}==0 && ${Target.Distance}<${Math.Calc[${NadoxShieldRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Clicking ${NadoxShield}
    /call cast ${NadoxShield} item
    /return
  }

  /if (${Defined[ZenurixEar]} && ${FindItem[${ZenurixEar}].Timer}==0 && ${Target.Distance}<${Math.Calc[${ZenurixEarRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Clicking ${ZenurixEar}
    /call cast ${ZenurixEar} item
    /return
  }

  /if (!${Defined[Vule]} && ${FindItemCount[${BoomStick}]} && ${FindItem[${BoomStick}].Timer}==0 && ${Target.Distance}<${Math.Calc[${BoomStickRange}*${RangeMod}]}) {
    /echo [${Time.Time24}] Clicking ${BoomStick}
    /call cast ${BoomStick} item
    /return
  }

  /if (${Target.Distance}<${Math.Calc[${Spell[${Nuke}].Range}*${RangeMod}]}) {
    /call WaitForSpellReady ${Nuke}
    /call cast ${Nuke}
    /return
  }

/return

Sub Burn
  /if (${Defined[EggKeeperCharm]} && ${FindItem[${EggKeeperCharm}].Timer}==0) {
    /echo [${Time.Time24}] Clicking ${EggKeeperCharm}
    /call cast ${EggKeeperCharm} item
  }

  /varset Action Combat
/return

Sub Pet
  /if (${Me.Pet.ID} && ${PurgeCrapBuffsTimer}==0) {
    /call BlockBuff "Aura of the Witchdoctor" "Pet"
    /call BlockBuff "Aura of the Bloodmage" "Pet"
    /call BlockBuff "Aura of the Glyph Wielder" "Pet"
    /call BlockBuff "Brell's Barrier Rk. II" "Pet"
    /call BlockBuff "Brell's Barrier Rk. III" "Pet"
    /call BlockBuff "Reflective Foresight Rk. II" "Pet"
    /call BlockBuff "Reflective Foresight Rk. III" "Pet"
    /call BlockBuff "Guardian Rune" "Pet"
    /varset PurgeCrapBuffsTimer 600
  }

  /if (${Defined[KhasvaPetEar]} && !${Me.Buff[${KhasvaPetEarBuff}].ID} && ${KhasvaPetEarTimer}==0) {
    /echo [${Time.Time24}] Clicking ${KhasvaPetEar}
    /call cast ${KhasvaPetEar} item
    /if (${Macro.Return.Equal[CAST_SUCCESS]}) {
      /varset KhasvaPetEarTimer 18000
    } else {
      /varset KhasvaPetEarTimer 1200
    }
  }

  /if (${Defined[DraekonStatue]} && !${Me.Pet.ID} && ${DraekonStatueTimer}==0 && !${Spawn[npc The Chaos].ID}) {
    /echo [${Time.Time24}] Summoning a pet with ${DraekonStatue}
    /declare OrigRanged int local ${Me.Inventory[11].ID}
    /call cast ${DraekonStatue} item
    /delay 10
    /if (!${Macro.Return.Equal[CAST_INTERRUPTED]}) {
      /varset DraekonStatueTimer 6000
    }
    /if (!${Me.Pet.ID}) {
      /varset DraekonStatueTimer 1200
    } else {
      /if (${Defined[KhasvaPetEar]}) {
        /if (${Me.Buff[${KhasvaPetEarBuff}].ID}) {
          /echo [${Time.Time24}] Pet is powered up with ${KhasvaPetEarBuff}
          /varset KhasvaPetEarPower TRUE
        } else {
          /varset KhasvaPetEarPower FALSE
        }
      }
      /echo [${Time.Time24}] Pet alive!
      /pet focus on
      /pet hold
      /pet no taunt
      /varset NewTarget TRUE
    }
  /return
  }

  /if (${Defined[PetHasteStaff]} && !${Me.PetBuff[${PetHasteStaffBuff}]} && ${Me.Pet.ID}) {
    /if (!${Target.ID}) /squelch /tar ID ${Me.ID}
    /call cast ${PetHasteStaff} item
    /return
  }

/return

Sub ShortBuff
  /declare SubAction string local ${Action}
  /if (${Me.Gem[Mana Inferno Rk. I]}>0 && ${Group}) {
    /for j 0 to ${Group}
      /if (${ChangeAction}) /return
      /doevents
      /if (${Spawn[pc ${Group.Member[${j}]}].ID}) {
        /squelch /tar ID ${Spawn[pc ${Group.Member[${j}]}].ID}
        /delay 3
        /echo [${Time.Time24}] Casting ${InfernoBuff} on ${Group.Member[${j}]}
        /call WaitForSpellReady ${InfernoBuff}
        /call cast ${InfernoBuff}
      }
    /next j
  } else {
    /for j 1 to ${GroupLead.Size}
      /if (${ChangeAction}) /return
      /doevents
      /if (${Spawn[pc ${GroupLead[${j}]}].ID}) {
        /squelch /tar ID ${Spawn[pc ${GroupLead[${j}]}].ID}
        /echo [${Time.Time24}] Buffing ${Target.CleanName} (${j} of ${GroupLead.Size}) with ${InfernoBuff}
        /call WaitForSpellReady ${InfernoBuff}
        /call cast ${InfernoBuff}
      } else {
        /echo [${Time.Time24}] ${GroupLead[${j}]} not in zone.
      }
    /next j
  }
  /if (!${ChangeAction} && ${Action.Equal[ShortBuff]}) {
    /echo [${Time.Time24}] Done with shortbuffs.
    /varset ChangeAction TRUE
    /varset Action Nothing
  }
/return

Sub LongBuff
  /declare SubAction string local ${Action}
  /echo [${Time.Time24}] Buffing ${ManaBuff}
  /for j 1 to ${GroupLead.Size}
    /if (${Action.NotEqual[LongBuff]}) /return
    /doevents
    /if (${Spawn[pc ${GroupLead[${j}]}].ID}) {
      /squelch /tar ID ${Spawn[pc ${GroupLead[${j}]}].ID}
      /echo [${Time.Time24}] Buffing ${Target.CleanName} (${j} of ${GroupLead.Size})
      /call WaitForSpellReady ${ManaBuff}
      /call cast ${ManaBuff}
    } else {
      |/echo [${Time.Time24}] ${GroupLead[${j}]} not in zone.
    }
  /next j
  /echo [${Time.Time24}] Done with longbuffs.
  /if (!${ChangeAction} && ${Action.Equal[LongBuff]}) {
    /varset ChangeAction TRUE
    /varset Action Nothing
  }
/return

Sub DetectSpells

|----Detect Spells------------------------------------------------------
    /call FindRank "Entwining Thoughts Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[Nuke]}) /declare Nuke string outer ${Macro.Return}

    /call FindRank "Euphoric Noose Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[Dot]}) /declare Dot string outer ${Macro.Return}

    /call FindRank "Euphoric Spasm Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[MeleeVuln]}) /declare MeleeVuln string outer ${Macro.Return}

    /call FindRank "Overbearing Power Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[SpellVuln]}) /declare SpellVuln string outer ${Macro.Return}

    /call FindRank "Mana Inferno Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[InfernoBuff]}) /declare InfernoBuff string outer ${Macro.Return}

    /call FindRank "Visions of Power Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[TwincastBuff]}) /declare TwincastBuff string outer ${Macro.Return}

    /call FindRank "Insight of the Seer Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[ManaBuff]}) /declare ManaBuff string outer ${Macro.Return}

    /call FindRank "Wrath of the Phantasmist"
    /if (${Macro.Return.Length}>3 && !${Defined[ResistDebuff]}) /declare ResistDebuff string outer ${Macro.Return}

    /call FindRank "Euphoric Bliss Rk."
    /if (${Macro.Return.Length}>3 && !${Defined[Mez]}) /declare Mez string outer ${Macro.Return}

/return
